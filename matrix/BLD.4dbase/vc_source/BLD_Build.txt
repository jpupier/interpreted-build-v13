//%attributes = {"lang":"en","shared":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 05/24/12, 12:45:40
// ----------------------------------------------------
// Description:
//   Build a new component version.
//
//   A "Build" is really a file copy of the interpreted
//   structure and a few other files, plus a simple
//   versioning system.
//
//   The reason the BUILD APPLICATION command is not
//   used (nor the feature) is this must be an
//   interpreted component; 4D's build feature only
//   creates compiled components.
// --[HEADEREND]---------------------------------------
C_TEXT($1;$componentFolderName_t)
C_TEXT($2;$projectRoot_t)

C_TEXT($componentFolder_t)
C_TEXT($databasePackage_t)
C_TEXT($sourceMacros_t)
C_TEXT($sourceResources_t)
C_TEXT($sourceBin_t)

C_BLOB($versionFile_b)

If (BLD_CheckForMiscI )

CONFIRM("This will quit 4D in order to complete the build, okay?")

If (OK=1)

// Handle parameters
If (Count parameters>=1)
$componentFolderName_t:=$1
Else 
$componentFolderName_t:="Components"
End if 

If (Count parameters>=2)
$projectRoot_t:=$2
Else 
// Default Project root is 2 levels up from the database folder because I usually store the source in "matrix".
$projectRoot_t:=UTIL_FILE_GetParent (UTIL_FILE_GetParent (Get 4D folder(Database Folder ;*)))
End if 

$databasePackage_t:=UTIL_FILE_GetDatabaseName ("host")+".4dbase"

// This folder contains the output interpreted component.
$componentFolder_t:=$projectRoot_t+$componentFolderName_t+Folder separator +$databasePackage_t+Folder separator 

// This folder contains necessary macros to run the component, if any.
$sourceMacros_t:=Get 4D folder(Database Folder ;*)+"Macros v2"+Folder separator 

// This folder contains resources, if any.
$sourceResources_t:=Get 4D folder(Current Resources folder ;*)

// This folder contains the script files necessary to complete a build.
// NOTE: only applies to this component, will be skipped for hosts.
$sourceBin_t:=Get 4D folder(Database Folder ;*)+"bin"+Folder separator 

//===============================================================
// Build actions

// Delete old component
UTIL_FILE_DeleteFolder ($componentFolder_t)

// Mainly seems to be a problem in Windows with the folder taking
// some time to be "really" deleted (i.e. the next call to
// CREATE FOLDER can fail).  Might wrap CREATE FOLDER to trap the
// error and try again if this does not work.
DELAY PROCESS(Current process;10)

// Create new component folder
CREATE FOLDER($componentFolder_t;*)

// Copy Macros V2 folder if it exists
If (Test path name($sourceMacros_t)=Is a folder )
COPY DOCUMENT($sourceMacros_t;$componentFolder_t)
End if 

// Copy Resources folder
If (Test path name($sourceResources_t)=Is a folder )
COPY DOCUMENT($sourceResources_t;$componentFolder_t)
End if 

// Copy bin folder
If (Test path name($sourceBin_t)=Is a folder )
COPY DOCUMENT($sourceBin_t;$componentFolder_t)
End if 

// Create versioning file
$versionFile_b:=BLD_GetVersionFile 
BLOB TO DOCUMENT($componentFolder_t+"version.txt";$versionFile_b)

// Execute a script that will copy files after 4D has quit.
BLD_Finalize ($projectRoot_t;$componentFolderName_t)

QUIT 4D
End if 
Else 
ALERT("You cannot build without the MISC I plug-in.")
End if 
